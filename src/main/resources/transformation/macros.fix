do put_macro("copy-all-items")
  set_array("$[target][]")
  do list(path: "$[source]", "var": "$copy-all-items")
    copy_field("$copy-all-items", "$[target][].$append")
  end
end

do put_macro("copy-first-item")
  if exists("$[source].1")
    copy_field("$[source].1", "@copy-first-item")
  else
    copy_field("$[source]", "@copy-first-item")
  end
  move_field("@copy-first-item", "$[target]")
end

do put_macro("copy-substring", length: "1")
  call_macro("substring", source: "$[source]", target: "@copy-substring", start: "$[start]", length: "$[length]")
  move_field("@copy-substring", "$[target]")
end

do put_macro("lobid-gnd")
  copy_field("$[source]", "@lobid-gnd")
  lookup("@lobid-gnd", "lobid-gnd", delete: "true")
  split_field("@lobid-gnd", "\\u001F")
  do list(path: "@lobid-gnd", "var": "$lobid-gnd")
    if exists("$[target].preferredName")
      copy_field("$lobid-gnd", "$[target].variantName[].$append")
    else
      copy_field("$lobid-gnd", "$[target].preferredName")
    end
  end
  remove_field("@lobid-gnd")
end

do put_macro("online-access")
  add_field("OnlineAccess[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.u", target: "OnlineAccess[].$last.uri")
  call_macro("copy-first-item", source: "$i.x", target: "OnlineAccess[].$last.nonpublicnote")
  call_macro("copy-first-item", source: "$i.z", target: "OnlineAccess[].$last.publicnote")
  copy_field("$i.3", "OnlineAccess[].$last.relatedto")
  copy_field("$i.m", "$i.@contact")
  set_array("OnlineAccess[].$last.contact[]")
  do list(path: "$i.@contact", "var": "$j")
    split_field("$j", ";")
    do list(path: "$j", "var": "$k")
      copy_field("$k", "OnlineAccess[].$last.contact[].$append")
    end
  end
  copy_field("$i.q", "OnlineAccess[].$last.format")
end

do put_macro("remove-nonsort")
  if is_array("$[source]")
    call_macro("remove-nonsort-string", source: "$[source].*")
  else
    call_macro("remove-nonsort-string", source: "$[source]")
  end
end

do put_macro("remove-nonsort-string")
  replace_all("$[source]", "[\\u0098\\u009C\\u00AC]", "")
  replace_all("$[source]", "<<(.*?)>>", "$1")
  #replace_all("$[source]", "<(.*?)>", "[$1]")
end

do put_macro("subject-common")
  set_array("@combined_label")
  copy_field("$i.a", "@combined_label.$append")
  copy_field("$i.b", "@combined_label.$append")
  join_field("@combined_label", " ")
  split_field("@combined_label", ";|--")
  replace_all("@combined_label.*", "(?<!\\p{Upper})[.]$|[,]$", "")
  join_field("@combined_label", ", ")
  if exists("$i.c")
    paste("@combined_label", "@combined_label", "~, ", "$i.c", join_char: "")
  end
  if exists("$i.d")
    paste("@combined_label", "@combined_label", "~ (", "$i.d", "~)", join_char: "")
  end
  if exists("$i.g")
    paste("@combined_label", "@combined_label", "~ (", "$i.g", "~)", join_char: "")
  end
  if exists("$i.x")
    paste("@combined_label", "@combined_label", "~/", "$i.x")
  end
  replace_all("@combined_label", "([a-z])[.]$", "$1")
  call_macro("remove-nonsort", source: "@combined_label")
  copy_field("@combined_label", "@rswk.combinedLabel")
  move_field("@combined_label", "introx.subject[].$append")
  set_array("@rswk.subjectIdentifier[]")
  set_array("@rswk.identifierGND[]")
  set_array("@rswk.variantName[]")
  do list(path: "$i.0", "var": "$j")
    copy_field("$j", "@rswk.subjectIdentifier[].$append")
    if any_match("$j", "\\(DE-588\\).*")
      replace_all("$j", "^\\(DE-588\\)", "")
      copy_field("$j", "@rswk.identifierGND[].$append")
      call_macro("lobid-gnd", source: "$j", target: "@rswk")
    end
  end
  replace_all("@rswk.subjectAddendum", "[.]$", "")
  replace_all("@rswk.subjectChronological", "[.]$", "")
  replace_all("@rswk.subjectConference", "[.]$", "")
  replace_all("@rswk.subjectCorporateBodyName", "[.]$", "")
  replace_all("@rswk.subjectCorporateBodySubUnit", "[.]$", "")
  replace_all("@rswk.subjectDate", "\\s?[:.]?$", "")
  replace_all("@rswk.subjectGenre", "[.]$", "")
  replace_all("@rswk.subjectGeoAddendum", "^[(]|[.)]$", "")
  replace_all("@rswk.subjectGeoName", "[.]$", "")
  replace_all("@rswk.subjectNameAddendum", "^[(]|[.)]$", "")
  replace_all("@rswk.subjectNumbering", "^[(]|\\s?[);:]?$", "")
  replace_all("@rswk.subjectPersonName", "[,.]$", "")
  replace_all("@rswk.subjectTitleName", "[.]$", "")
  replace_all("@rswk.subjectUnit", "[.]$", "")
  call_macro("remove-nonsort", source: "@rswk.subjectPersonName")
  call_macro("remove-nonsort", source: "@rswk.subjectTitleName")
  call_macro("remove-nonsort", source: "@rswk.subjectTopicName")
  move_field("@rswk", "RSWK[].$append")
end

do put_macro("subject-personal-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.d", target: "@rswk.subjectDate")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectPersonName")
  call_macro("copy-first-item", source: "$i.t", target: "@rswk.subjectTitleName")
  call_macro("copy-first-item", source: "$i.c", target: "@rswk.subjectNameAddendum")
  call_macro("subject-common")
end

do put_macro("subject-corporate-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectCorporateBodyName")
  call_macro("copy-first-item", source: "$i.b", target: "@rswk.subjectCorporateBodySubUnit")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("copy-first-item", source: "$i.n", target: "@rswk.subjectNumbering")
  call_macro("copy-first-item", source: "$i.t", target: "@rswk.subjectTitleName")
  call_macro("copy-first-item", source: "$i.p", target: "@rswk.subjectUnit")
  call_macro("copy-first-item", source: "$i.c", target: "@rswk.subjectGeoAddendum")
  call_macro("subject-common")
end

do put_macro("subject-meeting-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectConference")
  call_macro("copy-first-item", source: "$i.d", target: "@rswk.subjectDate")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("copy-first-item", source: "$i.n", target: "@rswk.subjectNumbering")
  call_macro("copy-first-item", source: "$i.t", target: "@rswk.subjectTitleName")
  call_macro("copy-first-item", source: "$i.p", target: "@rswk.subjectUnit")
  call_macro("copy-first-item", source: "$i.c", target: "@rswk.subjectGeoAddendum")
  call_macro("subject-common")
end

do put_macro("subject-chronological-term")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectChronological")
  call_macro("subject-common")
end

do put_macro("subject-topical-term")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.a", target: "@subjectTopicName")
  split_field("@subjectTopicName", ";|--")
  set_array("@rswk.subjectTopicName[]")
  do list(path: "@subjectTopicName", "var": "$j")
    replace_all("$j", "[.]$", "")
    copy_field("$j", "@rswk.subjectTopicName[].$append")
  end
  remove_field("@subjectTopicName")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.x", target: "@rswk.subjectAddendum")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("subject-common")
end

do put_macro("subject-geographic-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("subject-common")
end

do put_macro("substring", length: "1")
  copy_field("$[source]", "@substring")
  substring("@substring", "$[start]", "$[length]")
  move_field("@substring", "$[target]")
end
