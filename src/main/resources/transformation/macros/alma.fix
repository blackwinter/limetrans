do put_macro("alma-deletion")
  add_field("$[deletion-literal]", "$[deletion-value]")
end

do put_macro("alma-index-identifier")
  copy_field("@mmsid", "RecordIdentifier.identifierForTheIndex")
end

do put_macro("alma-mms-and-isil")
  do list(path: "MBD  ", "var": "$i")
    if any_equal("$i.M", "$[member]")
      copy_field("$i.i", "@mmsiz")
    end
  end

  call_macro("alma-mms-to-isil", source: "001", target: "@isil")
  call_macro("alma-mms-to-isil", source: "@mmsiz", target: "@isiliz")

  paste("@mmsid", "~(", "@isil", "~)", "001", join_char: "")
end

do put_macro("alma-mms-to-isil")
  copy_field("$[source]", "@alma-mms-to-isil")
  parse_text("@alma-mms-to-isil", ".*(.{4})$")
  lookup("@alma-mms-to-isil", "institution-code-to-isil", delete: "true")
  move_field("@alma-mms-to-isil", "$[target]")
end

do put_macro("alma-mms-with-isil")
  set_array("$[target][]")
  do list(path: "$[source]", "var": "$alma-mms-with-isil")
    if any_match("$alma-mms-with-isil", "99\\d{10,}")
      call_macro("alma-mms-to-isil", source: "$alma-mms-with-isil", target: "@alma-mms-with-isil")
      paste("$[target][].$append", "~(", "@alma-mms-with-isil", "~)", "$alma-mms-with-isil", join_char: "")
      remove_field("@alma-mms-with-isil")
    else
      copy_field("$alma-mms-with-isil", "$[target][].$append")
    end
  end
end

do put_macro("alma-format-carrier", length: "1")
  call_macro("substring", source: "$i.A", target: "@alma-format-carrier", start: "$[start]", length: "$[length]")
  copy_field("@alma-format-carrier", "@alma-format-carrier-facet")
  lookup("@alma-format-carrier", "alma-format-carrier-$[start]", delete: "true")
  move_field("@alma-format-carrier", "FormatCarrier[].$append")
  lookup("@alma-format-carrier-facet", "alma-format-carrier-facet-$[start]", delete: "true")
  move_field("@alma-format-carrier-facet", "@facet_format.$append")
end

do put_macro("alma-type-monograph")
  call_macro("substring", source: "$i.A", target: "@alma-type-monograph", start: "$[start]")
  copy_field("@alma-type-monograph", "@alma-type-monograph-facet")
  lookup("@alma-type-monograph", "alma-type-monograph-$[start]", delete: "true")
  move_field("@alma-type-monograph", "TypeMonograph[].$append")
  lookup("@alma-type-monograph-facet", "alma-type-monograph-facet-$[start]", delete: "true")
  move_field("@alma-type-monograph-facet", "@facet_type.$append")
end

do put_macro("alma-type-periodical", length: "1")
  call_macro("substring", source: "$i.A", target: "@alma-type-periodical", start: "$[start]", length: "$[length]")
  copy_field("@alma-type-periodical", "@alma-type-periodical-facet")
  lookup("@alma-type-periodical", "alma-type-periodical-$[start]", delete: "true")
  move_field("@alma-type-periodical", "TypePeriodical[].$append")
  lookup("@alma-type-periodical-facet", "alma-type-periodical-facet-$[start]", delete: "true")
  move_field("@alma-type-periodical-facet", "@facet_type.$append")
end
