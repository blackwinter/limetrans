do put_macro("alma-mms-to-isil")
  copy_field("$[source]", "@alma-mms-to-isil")
  parse_text("@alma-mms-to-isil", ".*(.{4})$")
  lookup("@alma-mms-to-isil", "institution-code-to-isil", delete: "true")
  move_field("@alma-mms-to-isil", "$[target]")
end

do put_macro("alma-format-carrier", length: "1")
  call_macro("substring", source: "$i.A", target: "@alma-format-carrier", start: "$[start]", length: "$[length]")
  copy_field("@alma-format-carrier", "@alma-format-carrier-facet")
  lookup("@alma-format-carrier", "alma-format-carrier-$[start]", delete: "true")
  move_field("@alma-format-carrier", "FormatCarrier[].$append")
  lookup("@alma-format-carrier-facet", "alma-format-carrier-facet-$[start]", delete: "true")
  move_field("@alma-format-carrier-facet", "@facet_format.$append")
end

do put_macro("alma-subject-common")
  set_array("RSWK[].$last.subjectIdentifier[]")
  set_array("RSWK[].$last.identifierGND[]")
  do list(path: "$i.0", "var": "$j")
    copy_field("$j", "RSWK[].$last.subjectIdentifier[].$append")
    if any_match("$j", "\\(DE-588\\).*")
      copy_field("$j", "RSWK[].$last.identifierGND[].$append")
    end
  end
  replace_all("RSWK[].$last.identifierGND[]", "^\\(DE-588\\)", "")
  replace_all("RSWK[].$last.subjectAddendum", "[.]$", "")
  replace_all("RSWK[].$last.subjectChronological", "[.]$", "")
  replace_all("RSWK[].$last.subjectConference", "[.]$", "")
  replace_all("RSWK[].$last.subjectCorporateBodyName", "[.]$", "")
  replace_all("RSWK[].$last.subjectCorporateBodySubUnit", "[.]$", "")
  replace_all("RSWK[].$last.subjectDate", "\\s?[:.]?$", "")
  replace_all("RSWK[].$last.subjectGenre", "[.]$", "")
  replace_all("RSWK[].$last.subjectGeoAddendum", "^[(]|[.)]$", "")
  replace_all("RSWK[].$last.subjectGeoName", "[.]$", "")
  replace_all("RSWK[].$last.subjectNameAddendum", "^[(]|[.)]$", "")
  replace_all("RSWK[].$last.subjectNumbering", "^[(]|\\s?[);:]?$", "")
  replace_all("RSWK[].$last.subjectPersonName", "[,.]$", "")
  replace_all("RSWK[].$last.subjectTitleName", "[.]$", "")
  replace_all("RSWK[].$last.subjectUnit", "[.]$", "")
  call_macro("remove-nonsort", source: "RSWK[].$last.subjectPersonName")
  call_macro("remove-nonsort", source: "RSWK[].$last.subjectTitleName")
  call_macro("remove-nonsort", source: "RSWK[].$last.subjectTopicName")
end

do put_macro("alma-subject-personal-name")
  add_field("RSWK[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.z", target: "RSWK[].$last.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "RSWK[].$last.subjectChronological")
  call_macro("copy-first-item", source: "$i.d", target: "RSWK[].$last.subjectDate")
  call_macro("copy-first-item", source: "$i.v", target: "RSWK[].$last.subjectGenre")
  call_macro("copy-first-item", source: "$i.a", target: "RSWK[].$last.subjectPersonName")
  call_macro("copy-first-item", source: "$i.t", target: "RSWK[].$last.subjectTitleName")
  call_macro("copy-first-item", source: "$i.c", target: "RSWK[].$last.subjectNameAddendum")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-corporate-name")
  add_field("RSWK[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.z", target: "RSWK[].$last.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "RSWK[].$last.subjectChronological")
  call_macro("copy-first-item", source: "$i.a", target: "RSWK[].$last.subjectCorporateBodyName")
  call_macro("copy-first-item", source: "$i.b", target: "RSWK[].$last.subjectCorporateBodySubUnit")
  call_macro("copy-first-item", source: "$i.v", target: "RSWK[].$last.subjectGenre")
  call_macro("copy-first-item", source: "$i.n", target: "RSWK[].$last.subjectNumbering")
  call_macro("copy-first-item", source: "$i.t", target: "RSWK[].$last.subjectTitleName")
  call_macro("copy-first-item", source: "$i.p", target: "RSWK[].$last.subjectUnit")
  call_macro("copy-first-item", source: "$i.c", target: "RSWK[].$last.subjectGeoAddendum")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-meeting-name")
  add_field("RSWK[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.z", target: "RSWK[].$last.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "RSWK[].$last.subjectChronological")
  call_macro("copy-first-item", source: "$i.a", target: "RSWK[].$last.subjectConference")
  call_macro("copy-first-item", source: "$i.d", target: "RSWK[].$last.subjectDate")
  call_macro("copy-first-item", source: "$i.v", target: "RSWK[].$last.subjectGenre")
  call_macro("copy-first-item", source: "$i.n", target: "RSWK[].$last.subjectNumbering")
  call_macro("copy-first-item", source: "$i.t", target: "RSWK[].$last.subjectTitleName")
  call_macro("copy-first-item", source: "$i.p", target: "RSWK[].$last.subjectUnit")
  call_macro("copy-first-item", source: "$i.c", target: "RSWK[].$last.subjectGeoAddendum")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-chronological-term")
  add_field("RSWK[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.a", target: "RSWK[].$last.subjectChronological")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-topical-term")
  add_field("RSWK[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.a", target: "@subjectTopicName")
  split_field("@subjectTopicName", ";|--")
  set_array("RSWK[].$last.subjectTopicName")
  do list(path: "@subjectTopicName", "var": "$j")
    replace_all("$j", "[.]$", "")
    copy_field("$j", "RSWK[].$last.subjectTopicName.$append")
  end
  remove_field("@subjectTopicName")
  call_macro("copy-first-item", source: "$i.z", target: "RSWK[].$last.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "RSWK[].$last.subjectChronological")
  call_macro("copy-first-item", source: "$i.x", target: "RSWK[].$last.subjectAddendum")
  call_macro("copy-first-item", source: "$i.v", target: "RSWK[].$last.subjectGenre")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-geographic-name")
  add_field("RSWK[].$append.__dummy__", "")
  call_macro("copy-first-item", source: "$i.a", target: "RSWK[].$last.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "RSWK[].$last.subjectChronological")
  call_macro("copy-first-item", source: "$i.v", target: "RSWK[].$last.subjectGenre")
  call_macro("alma-subject-common")
end

do put_macro("alma-type-monograph")
  call_macro("substring", source: "$i.A", target: "@alma-type-monograph", start: "$[start]")
  copy_field("@alma-type-monograph", "@alma-type-monograph-facet")
  lookup("@alma-type-monograph", "alma-type-monograph-$[start]", delete: "true")
  move_field("@alma-type-monograph", "TypeMonograph[].$append")
  lookup("@alma-type-monograph-facet", "alma-type-monograph-facet-$[start]", delete: "true")
  move_field("@alma-type-monograph-facet", "@facet_type.$append")
end

do put_macro("alma-type-periodical", length: "1")
  call_macro("substring", source: "$i.A", target: "@alma-type-periodical", start: "$[start]", length: "$[length]")
  copy_field("@alma-type-periodical", "@alma-type-periodical-facet")
  lookup("@alma-type-periodical", "alma-type-periodical-$[start]", delete: "true")
  move_field("@alma-type-periodical", "TypePeriodical[].$append")
  lookup("@alma-type-periodical-facet", "alma-type-periodical-facet-$[start]", delete: "true")
  move_field("@alma-type-periodical-facet", "@facet_type.$append")
end
