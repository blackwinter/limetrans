do put_macro("alma-mms-to-isil")
  copy_field("$[source]", "@alma-mms-to-isil")
  parse_text("@alma-mms-to-isil", ".*(.{4})$")
  lookup("@alma-mms-to-isil", "institution-code-to-isil", delete: "true")
  move_field("@alma-mms-to-isil", "$[target]")
end

do put_macro("alma-format-carrier", length: "1")
  call_macro("substring", source: "$i.A", target: "@alma-format-carrier", start: "$[start]", length: "$[length]")
  copy_field("@alma-format-carrier", "@alma-format-carrier-facet")
  lookup("@alma-format-carrier", "alma-format-carrier-$[start]", delete: "true")
  move_field("@alma-format-carrier", "FormatCarrier[].$append")
  lookup("@alma-format-carrier-facet", "alma-format-carrier-facet-$[start]", delete: "true")
  move_field("@alma-format-carrier-facet", "@facet_format.$append")
end

do put_macro("alma-subject-common")
  set_array("@combined_label")
  copy_field("$i.a", "@combined_label.$append")
  copy_field("$i.b", "@combined_label.$append")
  join_field("@combined_label", " ")
  split_field("@combined_label", ";|--")
  replace_all("@combined_label.*", "(?<!\\p{Upper})[.]$|[,]$", "")
  join_field("@combined_label", ", ")
  if exists("$i.c")
    paste("@combined_label", "@combined_label", "~, ", "$i.c", join_char: "")
  end
  if exists("$i.d")
    paste("@combined_label", "@combined_label", "~ (", "$i.d", "~)", join_char: "")
  end
  if exists("$i.g")
    paste("@combined_label", "@combined_label", "~ (", "$i.g", "~)", join_char: "")
  end
  if exists("$i.x")
    paste("@combined_label", "@combined_label", "~/", "$i.x")
  end
  replace_all("@combined_label", "([a-z])[.]$", "$1")
  call_macro("remove-nonsort", source: "@combined_label")
  copy_field("@combined_label", "@rswk.combinedLabel")
  move_field("@combined_label", "introx.subject[].$append")
  set_array("@rswk.subjectIdentifier[]")
  set_array("@rswk.identifierGND[]")
  do list(path: "$i.0", "var": "$j")
    copy_field("$j", "@rswk.subjectIdentifier[].$append")
    if any_match("$j", "\\(DE-588\\).*")
      copy_field("$j", "@rswk.identifierGND[].$append")
    end
  end
  replace_all("@rswk.identifierGND[].*", "^\\(DE-588\\)", "")
  replace_all("@rswk.subjectAddendum", "[.]$", "")
  replace_all("@rswk.subjectChronological", "[.]$", "")
  replace_all("@rswk.subjectConference", "[.]$", "")
  replace_all("@rswk.subjectCorporateBodyName", "[.]$", "")
  replace_all("@rswk.subjectCorporateBodySubUnit", "[.]$", "")
  replace_all("@rswk.subjectDate", "\\s?[:.]?$", "")
  replace_all("@rswk.subjectGenre", "[.]$", "")
  replace_all("@rswk.subjectGeoAddendum", "^[(]|[.)]$", "")
  replace_all("@rswk.subjectGeoName", "[.]$", "")
  replace_all("@rswk.subjectNameAddendum", "^[(]|[.)]$", "")
  replace_all("@rswk.subjectNumbering", "^[(]|\\s?[);:]?$", "")
  replace_all("@rswk.subjectPersonName", "[,.]$", "")
  replace_all("@rswk.subjectTitleName", "[.]$", "")
  replace_all("@rswk.subjectUnit", "[.]$", "")
  call_macro("remove-nonsort", source: "@rswk.subjectPersonName")
  call_macro("remove-nonsort", source: "@rswk.subjectTitleName")
  call_macro("remove-nonsort", source: "@rswk.subjectTopicName")
  move_field("@rswk", "RSWK[].$append")
end

do put_macro("alma-subject-personal-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.d", target: "@rswk.subjectDate")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectPersonName")
  call_macro("copy-first-item", source: "$i.t", target: "@rswk.subjectTitleName")
  call_macro("copy-first-item", source: "$i.c", target: "@rswk.subjectNameAddendum")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-corporate-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectCorporateBodyName")
  call_macro("copy-first-item", source: "$i.b", target: "@rswk.subjectCorporateBodySubUnit")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("copy-first-item", source: "$i.n", target: "@rswk.subjectNumbering")
  call_macro("copy-first-item", source: "$i.t", target: "@rswk.subjectTitleName")
  call_macro("copy-first-item", source: "$i.p", target: "@rswk.subjectUnit")
  call_macro("copy-first-item", source: "$i.c", target: "@rswk.subjectGeoAddendum")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-meeting-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectConference")
  call_macro("copy-first-item", source: "$i.d", target: "@rswk.subjectDate")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("copy-first-item", source: "$i.n", target: "@rswk.subjectNumbering")
  call_macro("copy-first-item", source: "$i.t", target: "@rswk.subjectTitleName")
  call_macro("copy-first-item", source: "$i.p", target: "@rswk.subjectUnit")
  call_macro("copy-first-item", source: "$i.c", target: "@rswk.subjectGeoAddendum")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-chronological-term")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectChronological")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-topical-term")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.a", target: "@subjectTopicName")
  split_field("@subjectTopicName", ";|--")
  set_array("@rswk.subjectTopicName")
  do list(path: "@subjectTopicName", "var": "$j")
    replace_all("$j", "[.]$", "")
    copy_field("$j", "@rswk.subjectTopicName.$append")
  end
  remove_field("@subjectTopicName")
  call_macro("copy-first-item", source: "$i.z", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.x", target: "@rswk.subjectAddendum")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("alma-subject-common")
end

do put_macro("alma-subject-geographic-name")
  set_hash("@rswk")
  call_macro("copy-first-item", source: "$i.a", target: "@rswk.subjectGeoName")
  call_macro("copy-first-item", source: "$i.y", target: "@rswk.subjectChronological")
  call_macro("copy-first-item", source: "$i.v", target: "@rswk.subjectGenre")
  call_macro("alma-subject-common")
end

do put_macro("alma-type-monograph")
  call_macro("substring", source: "$i.A", target: "@alma-type-monograph", start: "$[start]")
  copy_field("@alma-type-monograph", "@alma-type-monograph-facet")
  lookup("@alma-type-monograph", "alma-type-monograph-$[start]", delete: "true")
  move_field("@alma-type-monograph", "TypeMonograph[].$append")
  lookup("@alma-type-monograph-facet", "alma-type-monograph-facet-$[start]", delete: "true")
  move_field("@alma-type-monograph-facet", "@facet_type.$append")
end

do put_macro("alma-type-periodical", length: "1")
  call_macro("substring", source: "$i.A", target: "@alma-type-periodical", start: "$[start]", length: "$[length]")
  copy_field("@alma-type-periodical", "@alma-type-periodical-facet")
  lookup("@alma-type-periodical", "alma-type-periodical-$[start]", delete: "true")
  move_field("@alma-type-periodical", "TypePeriodical[].$append")
  lookup("@alma-type-periodical-facet", "alma-type-periodical-facet-$[start]", delete: "true")
  move_field("@alma-type-periodical-facet", "@facet_type.$append")
end
